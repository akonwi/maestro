use ard/async
use ard/dates
use ard/decode
use ard/duration
use ard/env
use ard/fs
use ard/io
use ard/json
use ard/maybe
use ard/sql

use maestro/db as _db
use maestro/fapi
use maestro/fixtures
use maestro/leagues
use maestro/odds

let decode_id = decode::field("id", decode::int)
let decode_name = decode::field("name", decode::string)

struct Snapshot {
	id: Int,
	name: Str,
	num_games: Int,
	wins: Int,
	losses: Int,
	draws:  Int,

	goals_against: Int,
	goals_for: Int,
	one_plus_scored: Int, // number of games in which team scored > 1 goal
	strike_rate: Float, // percentage of matches finding the net
	goals_diff: Int,
	xgf: Float,
	xga: Float,
	leakiness: Float,

	cleansheets: Int,
	one_conceded: Int,

	// could be presented as +1.5
	two_plus_conceded: Int,

	win_rate: Float,
}

impl Snapshot {
  fn to_dyn() Dynamic {
    Dynamic::object([
      "num_games": Dynamic::from(@num_games),
      "wins": Dynamic::from(@wins),
      "losses": Dynamic::from(@losses),
      "draws": Dynamic::from(@draws),
      "goals_against": Dynamic::from(@goals_against),
      "goals_for": Dynamic::from(@goals_for),
      "one_plus_scored": Dynamic::from(@one_plus_scored),
      "strike_rate": Dynamic::from(@strike_rate),
      "goals_diff": Dynamic::from(@goals_diff),
      "xgf": Dynamic::from(@xgf),
      "xga": Dynamic::from(@xga),
      "leakiness": Dynamic::from(@leakiness),
      "cleansheets": Dynamic::from(@cleansheets),
      "one_conceded": Dynamic::from(@one_conceded),
      "two_plus_conceded": Dynamic::from(@two_plus_conceded),
      "win_rate": Dynamic::from(@win_rate),
    ])
  }
}

struct Comparison {
  home: Snapshot,
  away: Snapshot,
}

fn get_comparison(db: sql::Database, fixture_id: Int) Comparison!Str {
  let body = try fapi::get_predictions(db, fixture_id)

  fn decode_stats(data: Dynamic) Snapshot![decode::Error] {
    let id = try decode_id(data)
    let name = try decode_name(data)
    let league_data = try decode::extract_field(data, "league") -> found {
      Result::err([decode::Error{
        expected: "Field",
        found: found,
        path: ["league"]
      }])
    }
    let num_played = try decode::run(league_data, decode::path(["fixtures", "played", "total"], decode::int))
    let wins = try decode::run(league_data, decode::path(["fixtures", "wins", "total"], decode::int))
    let losses = try decode::run(league_data, decode::path(["fixtures", "loses", "total"], decode::int))
    let draws = try decode::run(league_data, decode::path(["fixtures", "draws", "total"], decode::int))
    let goals_against = try decode::run(league_data, decode::path(["goals", "against", "total", "total"], decode::int))
    let xga = Float::from_int(goals_against) / Float::from_int(num_played)
    let goals_for = try decode::run(league_data, decode::path(["goals", "for", "total", "total"], decode::int))
    let xgf = Float::from_int(goals_for) / Float::from_int(num_played)
    let one_plus_scored = try decode::run(league_data, decode::path(["goals", "for", "under_over", "1.5", "over"], decode::int))
    let no_score = try decode::run(league_data, decode::path(["failed_to_score", "total"], decode::int))
    let cleansheets = try decode::run(league_data, decode::path(["clean_sheet", "total"], decode::int))
    let at_least_1_against = try decode::run(league_data, decode::path(["goals", "against", "under_over", "0.5", "over"], decode::int))
    let over_1_against = try decode::run(league_data, decode::path(["goals", "against", "under_over", "1.5", "over"], decode::int))
    let one_conceded = at_least_1_against - over_1_against
    let two_plus_conceded = over_1_against

    Result::ok(Snapshot{
      id: id,
      name: name,
      num_games: num_played,
      wins: wins,
      losses: losses,
      draws: draws,
      goals_against: goals_against,
      xga: xga,
      goals_for: goals_for,
      one_plus_scored: one_plus_scored,
      strike_rate: 1.0 - Float::from_int(no_score) / Float::from_int(num_played),
      xgf: xgf,
      goals_diff: goals_for - goals_against,
      cleansheets: cleansheets,
      one_conceded: one_conceded,
      two_plus_conceded: two_plus_conceded,
      leakiness: Float::from_int(two_plus_conceded) / Float::from_int(num_played) * 100.0,
      win_rate: Float::from_int(wins) / Float::from_int(num_played),
    })
  }

  fn decode_comparison(data: Dynamic) Comparison![decode::Error] {
    let home = try decode::run(data, decode::field("home", decode_stats))
    let away = try decode::run(data, decode::field("away", decode_stats))

    Result::ok(Comparison{
      home: home,
      away: away,
    })
  }

  let comparison = try decode::run(body, decode::field("response", decode::at(0, decode::field("teams", decode_comparison)))) -> errs {
    Result::err(errs.at(0).to_str())
  }

  Result::ok(comparison)
}

// returns metrics over the last 5 games before the given timestamp
fn get_form(db: sql::Database, team_id: Int, before: Int) Snapshot!Str {
  let rows = try db.query("SELECT
    f.id,
    l.id as league_id,
    f.season,
    f.home_id,
    f.away_id,
    f.timestamp,
    f.finished,
    f.home_goals,
    f.away_goals,
    h.name as home_name,
    a.name as away_name,
    l.name as league_name,
    team_stats.xg as team_xg,
    opp_stats.xg as opp_xg
  FROM fixtures f
  INNER JOIN leagues l ON l.id = f.league_id
  INNER JOIN teams h ON h.id = f.home_id
  INNER JOIN teams a ON a.id = f.away_id
  LEFT JOIN fixture_stats team_stats ON team_stats.fixture_id = f.id AND team_stats.team_id = @team_id
  LEFT JOIN fixture_stats opp_stats ON opp_stats.fixture_id = f.id AND opp_stats.team_id != @team_id
  WHERE (f.home_id = @team_id OR f.away_id = @team_id)
  AND f.finished = true
  AND f.timestamp < @before
  ORDER BY f.timestamp DESC
  LIMIT 5")
    .all(["team_id": team_id, "before": before])

  mut fixtures_list: [fixtures::Fixture] = []
  mut xg_for_total = 0.0
  mut xg_against_total = 0.0

  for row, idx in rows {
    let fixture = try fixtures::from_row(row) -> errs {
      Result::err(errs.at(0).to_str())
    }
    fixtures_list.push(fixture)

    // Accumulate xG values from fixture_stats
    let team_xg = decode::run(row, decode::field("team_xg", decode::float)).or(0.0)
    let opp_xg = decode::run(row, decode::field("opp_xg", decode::float)).or(0.0)
    xg_for_total =+ team_xg
    xg_against_total =+ opp_xg
  }

  mut wins = 0
  mut losses = 0
  mut draws = 0
  mut goals_for = 0
  mut goals_against = 0
  mut one_plus_scored = 0
  mut cleansheets = 0
  mut failed_to_score = 0
  mut at_least_1_against = 0
  mut two_plus_conceded = 0

  for fixture in fixtures_list {
    match fixture.home.id == team_id {
      true => {
        goals_for =+ fixture.home_goals
        goals_against =+ fixture.away_goals
        if fixture.home_goals == 0 { failed_to_score =+ 1 }
        if fixture.away_goals == 0 { cleansheets =+ 1 }
        if fixture.home_goals > 1 { one_plus_scored =+ 1 }
        if fixture.away_goals >= 1 { at_least_1_against =+ 1 }
        if fixture.away_goals >= 2 { two_plus_conceded =+ 1 }
        match {
          fixture.is_home_win() => { wins =+ 1 },
          fixture.is_away_win() => { losses =+ 1 },
          _ => { draws =+ 1 }
        }
      },
      false => {
        goals_for =+ fixture.away_goals
        goals_against =+ fixture.home_goals
        if fixture.away_goals == 0 { failed_to_score =+ 1 }
        if fixture.home_goals == 0 { cleansheets =+ 1 }
        if fixture.away_goals > 1 { one_plus_scored =+ 1 }
        if fixture.home_goals >= 1 { at_least_1_against =+ 1 }
        if fixture.home_goals >= 2 { two_plus_conceded =+ 1 }
        match {
          fixture.is_away_win() => { wins =+ 1 },
          fixture.is_home_win() => { losses =+ 1 },
          _ => { draws =+ 1 }
        }
      }
    }
  }

  let num_games = fixtures_list.size()
  match num_games == 0 {
    true => Result::ok(Snapshot{
      id: team_id,
      name: "",
      num_games: 0,
      wins: 0,
      losses: 0,
      draws: 0,
      goals_against: 0,
      goals_for: 0,
      one_plus_scored: 0,
      strike_rate: 0.0,
      goals_diff: 0,
      xgf: 0.0,
      xga: 0.0,
      leakiness: 0.0,
      cleansheets: 0,
      one_conceded: 0,
      two_plus_conceded: 0,
      win_rate: 0.0,
    }),
    false => {
      let games = Float::from_int(num_games)
      let xgf = xg_for_total / games
      let xga = xg_against_total / games
      let one_conceded = at_least_1_against - two_plus_conceded

      Result::ok(Snapshot{
        id: team_id,
        name: "",
        num_games: num_games,
        wins: wins,
        losses: losses,
        draws: draws,
        goals_against: goals_against,
        goals_for: goals_for,
        one_plus_scored: one_plus_scored,
        strike_rate: 1.0 - Float::from_int(failed_to_score) / games,
        goals_diff: goals_for - goals_against,
        xgf: xgf,
        xga: xga,
        leakiness: Float::from_int(two_plus_conceded) / games * 100.0,
        cleansheets: cleansheets,
        one_conceded: one_conceded,
        two_plus_conceded: two_plus_conceded,
        win_rate: Float::from_int(wins) / games,
      })
    }
  }
}

fn get_form_from_api(db: sql::Database, team_id: Int, league_id: Int) Snapshot!Str {
  let entries = try fapi::get_team_played_fixtures(db, team_id: team_id, league_id: league_id, last: maybe::some(5))

  mut fixture_stats_list: [fixtures::FixtureStats] = []
  for e in entries {
    let fixture_id = try decode::run(e, decode::path(["fixture", "id"], decode::int)) -> errs { Result::err(errs.at(0).to_str()) }

    let data = try fapi::get_fixture(db, fixture_id)
    let fs = try fixtures::decode_stats(data) -> errs { Result::err("Unable to decode fixture {fixture_id}: {fixtures::collapse_errors("", errs)}") }
    fixture_stats_list.push(fs)
  }

  mut wins = 0
  mut losses = 0
  mut draws = 0
  mut goals_for = 0
  mut goals_against = 0
  mut xg_for_total = 0.0
  mut xg_against_total = 0.0
  mut one_plus_scored = 0
  mut cleansheets = 0
  mut failed_to_score = 0
  mut at_least_1_against = 0
  mut two_plus_conceded = 0
  mut team_name = ""

  for fs in fixture_stats_list {
    let is_home = fs.fixture.home.id == team_id

    let team_stats = match is_home {
      true => fs.home,
      false => fs.away
    }
    let opp_stats = match is_home {
      true => fs.away,
      false => fs.home
    }

    if team_name == "" {
      team_name = match is_home {
        true => fs.fixture.home.name,
        false => fs.fixture.away.name
      }
    }

    let team_goals = match is_home {
      true => fs.fixture.home_goals,
      false => fs.fixture.away_goals
    }
    let opp_goals = match is_home {
      true => fs.fixture.away_goals,
      false => fs.fixture.home_goals
    }

    goals_for =+ team_goals
    goals_against =+ opp_goals
    xg_for_total =+ team_stats.xg
    xg_against_total =+ opp_stats.xg

    if team_goals == 0 { failed_to_score =+ 1 }
    if opp_goals == 0 { cleansheets =+ 1 }
    if team_goals > 1 { one_plus_scored =+ 1 }
    if opp_goals >= 1 { at_least_1_against =+ 1 }
    if opp_goals >= 2 { two_plus_conceded =+ 1 }

    match {
      team_goals > opp_goals => { wins =+ 1 },
      team_goals < opp_goals => { losses =+ 1 },
      _ => { draws =+ 1 }
    }
  }

  let num_games = fixture_stats_list.size()
  match num_games == 0 {
    true => Result::ok(Snapshot{
      id: team_id,
      name: "",
      num_games: 0,
      wins: 0,
      losses: 0,
      draws: 0,
      goals_against: 0,
      goals_for: 0,
      one_plus_scored: 0,
      strike_rate: 0.0,
      goals_diff: 0,
      xgf: 0.0,
      xga: 0.0,
      leakiness: 0.0,
      cleansheets: 0,
      one_conceded: 0,
      two_plus_conceded: 0,
      win_rate: 0.0,
    }),
    false => {
      let games = Float::from_int(num_games)
      let xgf = xg_for_total / games
      let xga = xg_against_total / games
      let one_conceded = at_least_1_against - two_plus_conceded

      Result::ok(Snapshot{
        id: team_id,
        name: team_name,
        num_games: num_games,
        wins: wins,
        losses: losses,
        draws: draws,
        goals_against: goals_against,
        goals_for: goals_for,
        one_plus_scored: one_plus_scored,
        strike_rate: 1.0 - Float::from_int(failed_to_score) / games,
        goals_diff: goals_for - goals_against,
        xgf: xgf,
        xga: xga,
        leakiness: Float::from_int(two_plus_conceded) / games * 100.0,
        cleansheets: cleansheets,
        one_conceded: one_conceded,
        two_plus_conceded: two_plus_conceded,
        win_rate: Float::from_int(wins) / games,
      })
    }
  }
}

// Returns form-based comparison if league is followed and both teams have â‰¥5 games
// falls back to normal comparison for unfollowed leagues
fn get_form_comparison(db: sql::Database, fixture: fixtures::Fixture) Comparison!Str {
  match leagues::is_following(db, fixture.league.id) {
    false => {
      let home_form = try get_form_from_api(db: db, team_id: fixture.home.id, league_id: fixture.league_id)
      let away_form = try get_form_from_api(db: db, team_id: fixture.away.id, league_id: fixture.league_id)

      Result::ok(Comparison{ home: home_form, away: away_form })
    },
    true => {
      let home_form = try get_form(db, fixture.home.id, fixture.timestamp)
      let away_form = try get_form(db, fixture.away.id, fixture.timestamp)

      match home_form.num_games >= 5 and away_form.num_games >= 5 {
        true => Result::ok(Comparison{ home: home_form, away: away_form }),
        false => Result::err("Insufficient games")
      }
    }
  }
}

struct FixturePicks {
  fixture: fixtures::Fixture,
  stats: [odds::Stat]
}

fn pick_outcome(odd: odds::Stat, comp: Comparison, mut entry: FixturePicks) {
  for pick in odd.values {
    if pick.odd >= -150 {
      if pick.name == "Home" and comp.home.goals_diff > 0 and comp.away.goals_diff < 0 {
        entry.stats.push(odds::Stat{
          id: odd.id,
          name: pick.name,
          values: [pick],
        })
      }
      if pick.name == "Away" and comp.away.goals_diff > 0 and comp.home.goals_diff < 0 {
        entry.stats.push(odds::Stat{
          id: odd.id,
          name: pick.name,
          values: [pick],
        })
      }
    }
  }
}

fn pick_team_goal_lines(odd: odds::Stat, stats: Snapshot, mut entry: FixturePicks) {
  mut lines: [odds::Line] = []
  for line in odd.values {
    if line.odd >= -150 {
      if line.name == "Over 1.5" and stats.xgf > 1.5 {
        lines.push(line)
      }
      if line.name == "Over 2.5" and stats.xgf >= 2.25 {
        lines.push(line)
      }
      if line.name == "Under 0.5" and stats.xgf <= 1.0 {
        lines.push(line)
      }
      if line.name == "Under 1.5" and stats.xgf <= 1.5 {
        lines.push(line)
      }
      if line.name == "Under 2.5" and stats.xgf < 2.5 {
        lines.push(line)
      }
    }
  }

  if lines.size() > 0 {
    entry.stats.push(odds::Stat{
      id: odd.id,
      name: odd.name,
      values: lines
    })
  }
}

fn pick_cleansheet_lines(odd: odds::Stat, stats: Snapshot, mut entry: FixturePicks) {
  mut lines: [odds::Line] = []
  for line in odd.values {
    if line.odd >= -150 {
      if line.name == "No" and (stats.xga >= 1.5 or stats.leakiness >= 0.5) {
        lines.push(line)
      }

      // be extremely conservative
      if line.name == "Yes" and stats.xga < 1.0 {
        lines.push(line)
      }
    }
  }

  if lines.size() > 0 {
    entry.stats.push(odds::Stat{
      id: odd.id,
      name: odd.name,
      values: lines
    })
  }
}

private fn save_juice(db: sql::Database, id: Str, raw: Str) Void!Str {
  io::print("Saving juice: {id}")
  let rows = try db.query("SELECT id FROM juice WHERE id = @id").first(["id": id])
  let sql = match rows.is_some() {
    true => "UPDATE juice SET raw_data = @raw WHERE id = @id",
    false => "INSERT INTO juice (id, raw_data) VALUES (@id, @raw)"
  }
  try db.query(sql).run(["id": id, "raw": raw]) -> err {
    let msg = "Error saving juice: {err}"
    io::print(msg)
    Result::err(msg)
  }
  Result::ok(())
}

private fn find_juice() {
  let db = _db::connect()
  let ignored_leagues = leagues::get_ignored(db)

  mut entries: [FixturePicks] = []
  for fixture in fixtures::find_today(db) {
    if not ignored_leagues.has(fixture.league.id) {
      match get_form_comparison(db, fixture) {
        ok(comp) => {
          mut entry = FixturePicks{
            fixture: fixture,
            stats: [],
          }
          // find odds, if team over/under makes sense, save it
          match odds::get(db, fixture.id) {
            ok(all_odds) => {
              for odd in all_odds {
                if odd.id == odds::OUTCOME  {
                  pick_outcome(odd, comp, entry)
                }

                if odd.id == odds::HOME_TOTAL_GOALS  {
                  pick_team_goal_lines(odd, comp.home, entry)
                }

                if odd.id == odds::AWAY_TOTAL_GOALS {
                  pick_team_goal_lines(odd, comp.away, entry)
                }

                if odd.id == odds::HOME_CLEANSHEET {
                  pick_cleansheet_lines(odd, comp.home, entry)
                }

                if odd.id == odds::AWAY_CLEANSHEET {
                  pick_cleansheet_lines(odd, comp.away, entry)
                }
              }
            },
            err => io::print("Error getting odds for fixture {fixture.id}: {err}")
          }

          if entry.stats.size() > 0 {
            entries.push(entry)
          }
        },
        err => io::print("Error getting matchup data for fixture {fixture.id}: {err}")
      }
    }
  }

  io::print("Found {entries.size()} good bets")

  match json::encode(entries) {
    ok(content) => {
      save_juice(db, dates::get_today(), content)
    },
    err => io::print("Error encoding juice data to JSON: {err}")
  }

  db.close()
}

fn find_juice_worker() {
  while {
    find_juice()
    async::sleep(duration::from_hours(2))
  }
}

fn get_juice(db: sql::Database, date: Str) Str!Str {
  let id = match date.is_empty() {
    true => dates::get_today(),
    false => date
  }

  let row = try db.query("select * from juice where id = @id").first(["id": id])
  match row {
    row => {
      let raw = try decode::run(row, decode::field("raw_data", decode::string)) -> errs {
        Result::err(errs.at(0).to_str())
      }

      Result::ok(raw)
    },
    _ => Result::ok("[]"),
  }
}
