use ard/decode
use ard/maybe
use ard/sql

use maestro/fapi

fn collapse_errors(prefix: Str, errors: [decode::Error]) Str {
  mut msg = prefix
  for e in errors {
    msg = "{msg}: {e}"
  }
  msg
}

struct Team { id: Int, name: Str }

impl Team {
  fn to_dyn() Dynamic {
    Dynamic::object([
      "id": Dynamic::from(@id),
      "name": Dynamic::from(@name),
    ])
  }
}

fn Team::from_row(row: Dynamic) Team!Str {
  let id = try decode::run(row, decode::field("id", decode::int)) -> errs { Result::err(errs.at(0).to_str()) }
  let name = try decode::run(row, decode::field("name", decode::string)) -> errs { Result::err(errs.at(0).to_str()) }

  Result::ok(Team{ id: id, name: name })
}

fn Team::from_api(data: Dynamic) Team!Str {
  let id = try decode::run(data, decode::path(["team", "id"], decode::int)) -> errs { Result::err(errs.at(0).to_str()) }
  let name = try decode::run(data, decode::path(["team", "name"], decode::string)) -> errs { Result::err(errs.at(0).to_str()) }

  Result::ok(Team{ id: id, name: name })
}

fn fetch(db: sql::Database, id: Int) Team?!Str {
  let entry = try fapi::get_team(db, id)
  let team = try Team::from_api(entry)
  Result::ok(maybe::some(team))
}

fn get(db: sql::Database, id: Int) Team?!Str {
  let found = try db.query("SELECT * from teams where id = @id").first(["id":id])
  match found {
    row => {
      let team = try Team::from_row(row)
      Result::ok(maybe::some(team))
    },
    _ => fetch(db, id)
  }
}
